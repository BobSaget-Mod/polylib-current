dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run to configure the
dnl	Makefile in this directory.

# Authors: Bart Kienhuis, Vincent Loechner
# Copyright (c) 2000 The Regents of the University of California.
# All rights reserved.
# 
# Permission is hereby granted, without written agreement and without
# license or royalty fees, to use, copy, modify, and distribute this
# software and its documentation for any purpose, provided that the
# above copyright notice and the following two paragraphs appear in all
# copies of this software.
# 
# IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY
# FOR DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES
# ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
# THE UNIVERSITY OF CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE SOFTWARE
# PROVIDED HEREUNDER IS ON AN "AS IS" BASIS, AND THE UNIVERSITY OF
# CALIFORNIA HAS NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES,
# ENHANCEMENTS, OR MODIFICATIONS.
# 
# 						PT_COPYRIGHT_VERSION_2
# 						COPYRIGHTENDKEY

dnl Process this file with autoconf to produce a configure script.
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl
AC_INIT(include/polylib/polylib.h)

VERSION=5.0

dnl Checks for typedefs, structures, and compiler characteristics.
AC_CANONICAL_SYSTEM 

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h unistd.h)


case "$target" in
  *hpux*)  
	LD="ld";	
	SHEXT="sl" ; 
	EXEC_SUFFIX=	
	SHAREDLIB_FLAG="-Wl,-B,immediate,-B,nonfatal,-B,verbose";     
	LDCONFIG=
	INSTALL_LIB="install-static install-shared" ;;
  i*-*-linux-gnu)  
	LD="gcc"
	SHEXT="so" ; 
	EXEC_SUFFIX=
	SHAREDLIB_FLAG="-shared"
	LDCONFIG=ldconfig
	INSTALL_LIB="install-static install-shared" ;;
  *-sun-solaris*)  
	LD="gcc"
	SHEXT="so" 
	EXEC_SUFFIX=	
	SHAREDLIB_FLAG="-G"
	LDCONFIG=
	INSTALL_LIB="install-static install-shared" ;;
  i*-pc-cygwin)   
	LD="ld -r"
	SHEXT="dll"
	SHAREDLIB_FLAG=""
	LIBS="$LIBS -lcygwin -lkernel32 -lcrtdll"
	EXEC_SUFFIX=.exe	
	LDCONFIG=
	EXTRA_FLAGS="-DCYGWIN"
	INSTALL_LIB="install-static" ;;
    *)  AC_MSG_ERROR([ARCH "$target" is not supported]) ;;
esac


dnl /*****************************************************************************/
dnl See if the only goal is to create an 'int', 'longint', or 'longlongint' library

AC_CHECK_SIZEOF(int,1)
case "$target" in
 i*-pc-cygwin)
	echo "... sorry 4"
	ac_cv_sizeof_int=4;;
esac
AC_CHECK_SIZEOF(long int,1)
case "$target" in
 i*-pc-cygwin)
	echo "... sorry 4"
	ac_cv_sizeof_long_int=4;;
esac
AC_CHECK_SIZEOF(long long int,1)
case "$target" in
 i*-pc-cygwin)
	echo "... sorry 8"
	ac_cv_sizeof_long_long_int=8;;
esac


AC_ARG_ENABLE(int-lib,[ --enable-int-lib         Enable that only an int library is constructed], only_int_lib=$enableval,only_int_lib=no)
AC_ARG_ENABLE(longint-lib,[ --enable-longint-lib     Enable that only a long int library is constructed (default)], only_longint_lib=$enableval,only_longint_lib=no)
AC_ARG_ENABLE(longlongint-lib,[ --enable-longlongint-lib Enable that only a long long int library is constructed], only_longlongint_lib=$enableval,only_longlongint_lib=no)
AC_ARG_ENABLE(gmpint-lib,[ --enable-gmpint-lib Enable that only a gmp int library is constructued], only_gmpint_lib=$enableval,only_gmpint_lib=no)
AC_ARG_ENABLE(allint-lib,[ --enable-allint-lib Enable that 32, 64 and gmp int library is constructued], allint_lib=$enableval,allint_lib=no)

INT_BITS=`expr $ac_cv_sizeof_long_int \* 8`
LONG_BITS=`expr $ac_cv_sizeof_long_long_int \* 8`
GMP_BITS=`expr gmp`
GMP_AFLAGS="-DGNUMP"


if test "$only_int_lib" = "yes"; then
	INT_AFLAGS=-DLINEAR_VALUE_IS_INT
	no_long=yes
elif test "$only_longint_lib" = "yes"; then
	INT_AFLAGS="-DLINEAR_VALUE_IS_LONG -DLINEAR_VALUE_PROTECT_MULTIPLY\
			-DLINEAR_VALUE_ASSUME_SOFTWARE_IDIV"
	no_long=yes
elif test "$only_longlongint_lib" = "yes"; then
	LONG_AFLAGS="-DLINEAR_VALUE_IS_LONGLONG -DLINEAR_VALUE_PROTECT_MULTIPLY\
			-DLINEAR_VALUE_ASSUME_SOFTWARE_IDIV"
elif test "$only_gmpint_lib" = "yes"; then
	LONG_AFLAGS="-DLINEAR_VALUE_IS_LONGLONG -DLINEAR_VALUE_PROTECT_MULTIPLY\
			-DLINEAR_VALUE_ASSUME_SOFTWARE_IDIV"
else

dnl /******************************************************************/
dnl Check the size of the int's.
dnl two sizes library can be constructed: size = INT or LONG.
dnl have to compute [size]BITS and [size]AFLAGS

INT_AFLAGS="-DLINEAR_VALUE_IS_INT"
AC_MSG_CHECKING(what the LONG representation is)
LONG_AFLAGS="-DLINEAR_VALUE_PROTECT_MULTIPLY \
		-DLINEAR_VALUE_ASSUME_SOFTWARE_IDIV"
no_long=no
if test "$ac_cv_sizeof_long_long_int" -gt "$ac_cv_sizeof_int"; then
	LONG_AFLAGS="$LONG_AFLAGS -DLINEAR_VALUE_IS_LONGLONG"
	LONG_BITS=`expr $ac_cv_sizeof_long_long_int \* 8`
	AC_MSG_RESULT([long long int, $LONG_BITS bits])
elif test "$ac_cv_sizeof_long_int" -gt "$ac_cv_sizeof_int"; then
	LONG_AFLAGS="$LONG_AFLAGS -DLINEAR_VALUE_IS_LONG"
	LONG_BITS=`expr $ac_cv_sizeof_long_int \* 8`
	AC_MSG_RESULT([long int, $LONG_BITS bits])
else
	AC_MSG_RESULT([NONE!])
	no_long=yes
fi
fi

if test "$only_int_lib" = "yes"; then
	LIBS_TO_BUILD=$INT_BITS
	EXEC_TO_BUILD=$INT_BITS
	LONG_BITS=none
elif test "$only_longint_lib" = "yes"; then
	LIBS_TO_BUILD=$INT_BITS
	EXEC_TO_BUILD=$INT_BITS
	LONG_BITS=none
elif test "$only_longlongint_lib" = "yes"; then
	LIBS_TO_BUILD=$LONG_BITS
	EXEC_TO_BUILD=$LONG_BITS
elif test "$only_gmpint_lib" = "yes"; then
	LIBS_TO_BUILD=$GMP_BITS
	EXEC_TO_BUILD=$GMP_BITS
elif test "$no_long" = "yes"; then
	LIBS_TO_BUILD=$INT_BITS
	EXEC_TO_BUILD=$INT_BITS
	LONG_BITS=none
elif test "$allint_lib" = "yes"; then
	LIBS_TO_BUILD="$INT_BITS $LONG_BITS $GMP_BITS"
	EXEC_TO_BUILD="$INT_BITS $LONG_BITS $GMP_BITS"
else
	LIBS_TO_BUILD="$LONG_BITS"
	EXEC_TO_BUILD="$LONG_BITS"
fi

AC_ARG_ENABLE(short-exec,[ --enable-short-exec      Enable that the int library is used by the executables (by default, the long library is used if built)],[ if test "$enableval" = yes; then
		EXEC_TO_BUILD=$INT_BITS
    fi ] )

dnl /*****************************************************************************/
EXEC_EXTRA_SUFFIX=$EXEC_SUFFIX
AC_ARG_ENABLE(extra-suffix,[ --enable-extra-suffix    Enable that the executables take an extra suffix depending on the library size],[ if test "$enableval" = yes; then
		EXEC_EXTRA_SUFFIX="\$(BITS)$EXEC_SUFFIX"
    fi ] )
AC_ARG_ENABLE(all-exec,[ --enable-all-exec        Enable that the two sized libraries are used to build two executables of each, which take an extra suffix depending on the library (by default, only the long library is used)],[ if test "$enableval" = yes; then
		EXEC_TO_BUILD="$INT_BITS $LONG_BITS"
		EXEC_EXTRA_SUFFIX="\$(BITS)$EXEC_SUFFIX"
    fi ] )


dnl /*****************************************************************************/
dnl install static/shared/none libraries (default : see below)
EXEC_EXTRA_LIBS="-lpolylib\$(BITS)"

if test "$INSTALL_LIB"="install-static"; then
	LIBSTYPE_TO_BUILD="lib-static"
else
	LIBSTYPE_TO_BUILD="lib-static lib-shared"
fi

AC_ARG_ENABLE(onlyshared,[ --enable-onlyshared      Enable that only a shared library is constructued], only_shared=$enableval,only_shared=no)
AC_ARG_ENABLE(onlystatic,[ --enable-onlystatic      Enable that only a static library is constructued], only_static=$enableval,only_static=no)

dnl See if the user does not want to install any library
AC_ARG_ENABLE(install-lib,[ --disable-install-lib    Disable installation of the library],doinstall=$enableval,doinstall=yes)


if test "$doinstall" = "no"; then
	INSTALL_LIB=""
	EXEC_EXTRA_LIBS="-L\$(OBJ_DIR) $EXEC_EXTRA_LIBS"
	LIBSTYPE_TO_BUILD="lib-static"
	if test "$only_shared" = "yes"; then
		LIBSTYPE_TO_BUILD="lib-shared"
	fi
	if test "$only_static" = "yes"; then
		LIBSTYPE_TO_BUILD="lib-static"
	fi
else
	EXEC_EXTRA_LIBS="-L$libdir $EXEC_EXTRA_LIBS"
	if test "$only_shared" = "yes"; then
		INSTALL_LIB="install-shared"
		LIBSTYPE_TO_BUILD="lib-shared"
	fi
	if test "$only_static" = "yes"; then
		INSTALL_LIB="install-static"
		LIBSTYPE_TO_BUILD="lib-static"
	fi
fi

AC_ARG_WITH(ldconfig-cache, [ --with-ldconfig-cache=FILE Run ldconfig -C <FILE>],
	[if test "x$LDCONFIG" = "xldconfig"; then
		LDCONFIG="$LDCONFIG -C $withval"
	fi])


dnl /*****************************************************************************/
dnl Check for GMP library
AC_MSG_CHECKING(whether to use GMP)
AC_ARG_WITH(libgmp, [ --with-libgmp            DIR Location of the GMP Distribution], gmp_package=$withval,gmp_package=yes)

GFLAGS="-DGNUMP"
EHRHART_MP=
MATRIX_MP=
EXTRA_LIBS=
EXTRA_INCLUDES=

if test "x$gmp_package" = "xno"; then
	AC_MSG_RESULT(no)
else
	if test "x$gmp_package" = "xyes"; then
		AC_CHECK_HEADER(gmp.h,
			AC_CHECK_LIB(gmp,main,
				[ GFLAGS="-DGNUMP"
				  EHRHART_MP="ehrhart_mp.c"
				  MATRIX_MP="matrix_mp.c"
				  EXTRA_LIBS="-lgmp $EXTRA_LIBS"]))
	else
dnl Check if the GMP directory is a valid location
		GMP_DIR=$gmp_package
		if test ! -d "$GMP_DIR"; then
	    	AC_ERROR(Directory given for GMP Distribution is not a directory)
		else
			GMP_INC=$GMP_DIR/include
			GMP_LIB=$GMP_DIR/lib
			if test ! -f $GMP_INC/gmp.h; then
		    	AC_ERROR(Can't find $GMP_INC/gmp.h)
			elif test -f $GMP_LIB/libgmp.so -o -f $GMP_LIB/libgmp.a -o -f $GMP_LIB/libgmp.$SHEXT; then
				GFLAGS="-DGNUMP"
				EXTRA_INCLUDES="-I$GMP_INC"
				EHRHART_MP="ehrhart_mp.c"
				MATRIX_MP="matrix_mp.c"
				EXTRA_LIBS="-L$GMP_LIB -lgmp $EXTRA_LIBS"
				AC_MSG_RESULT(yes)
			else
		    	AC_ERROR(Can't find $GMP_LIB/libgmp.so nor $GMP_LIB/libgmp.a nor $GMP_LIB/libgmp.$SHEXT)
			fi
		fi
	fi
fi


echo "---------------------------------------------------"
echo "You can choose either:"
echo "make libs:         to build the libraries"
echo "make install-libs: to install them (if required)"
echo "make:              to build the libs, install them, and build the executables"
echo "make install:      to build and install everything"
echo "---------------------------------------------------"


AC_SUBST(VERSION)

AC_SUBST(LD)
AC_SUBST(SHEXT)
AC_SUBST(SHAREDLIB_FLAG)
AC_SUBST(LIBS)
AC_SUBST(INSTALL_LIB)
AC_SUBST(LIBSTYPE_TO_BUILD)
AC_SUBST(LDCONFIG)

dnl define variants of polylib to build
AC_SUBST(LIBS_TO_BUILD)
AC_SUBST(EXEC_TO_BUILD)

dnl long/int bits and compilation flags
AC_SUBST(INT_BITS)
AC_SUBST(INT_AFLAGS)
AC_SUBST(LONG_BITS)
AC_SUBST(LONG_AFLAGS)
AC_SUBST(GMP_BITS)
AC_SUBST(GMP_AFLAGS)


dnl Used with GMP is selected
AC_SUBST(GFLAGS)
AC_SUBST(EXTRA_INCLUDES)
AC_SUBST(EHRHART_MP)
AC_SUBST(MATRIX_MP)
AC_SUBST(EXTRA_LIBS)

AC_SUBST(EXEC_EXTRA_SUFFIX)
AC_SUBST(EXEC_EXTRA_LIBS)

dnl Used with CYGWIN platform
AC_SUBST(EXTRA_FLAGS)


AC_OUTPUT(vars.mk)
